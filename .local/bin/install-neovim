#!/usr/bin/env nu

def main [--clean] {
	print $"(ansi green)\( ◕ ◡ ◕ \)(ansi reset)"

	let nvim_dir = "~/dev/vendor/neovim" | path expand
	let bin = "~/.local/bin/nvim" | path expand

	if ($nvim_dir | path exists | $in == false) {
		print $"(ansi green)Cloning neovim(ansi reset)"
		git clone git@github.com:neovim/neovim.git $nvim_dir
		cd $nvim_dir
	} else {
		cd $nvim_dir
		print $"(ansi green)Updating neovim(ansi reset)"
		git pull
	}

	print $"(ansi green)Building neovim(ansi reset)"

	if $clean {
		print $"(ansi blue)Clean install coming up...(ansi reset)"
		try { rm -r build/ }  # clear the CMake cache
	}

	zsh -c 'make CMAKE_BUILD_TYPE=Release CMAKE_EXTRA_FLAGS="-DCMAKE_INSTALL_PREFIX=$HOME/.local" && make install'

	print $'(ansi green)installed to(ansi reset) "($bin)"'

	if (which nvim | is-empty) {
		print $"(ansi yellow)WARNING:(ansi reset) nvim is not in your PATH"
		print $"(ansi green)neovim --version(ansi reset)"
		^$bin --version
	} else {
		nvim --version
	}
}

